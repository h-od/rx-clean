apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    def config = rootProject.extensions.getByName("ext")

    def major = config["major"]
    def minor = config["minor"]
    def patch = config["patch"]

    compileSdkVersion config["compileSdk"]
    buildToolsVersion config["buildTools"]

    defaultConfig {
        applicationId config["appId"]

        minSdkVersion config["minSdk"]
        targetSdkVersion config["targetSdk"]

        versionCode project.hasProperty('versionCode') ? versionCode.toInteger() : 1
        versionName "${major}.${minor}.${patch}"

        testApplicationId config["testAppId"]
        testInstrumentationRunner config["testInstrumentationRunner"]
    }

    buildTypes {
        debug {
            testCoverageEnabled true
            minifyEnabled false
//            signingConfig signingConfigs.debug
        }
        release {
            testCoverageEnabled false
            minifyEnabled true
            shrinkResources true
//            signingConfig signingConfigs.release

            proguardFiles fileTree(dir: "../config/proguard", include: ["*.pro"]).asList().toArray()
            proguardFile getDefaultProguardFile('proguard-android.txt')

            matchingFallbacks = ['debug']
        }
    }

    signingConfigs {
        debug {
            storeFile file('../config/signing/debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }

        release {
            storeFile file('../config/signing/release.jks')
            storePassword ""
            keyAlias "release"
            keyPassword ""
        }
    }
}

dependencies {
    def dependencies = rootProject.ext.dependencies
    def testDependencies = rootProject.ext.testDependencies

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(path: ':domain')

    implementation dependencies.kotlin

    implementation dependencies.appCompat
    implementation dependencies.design
    implementation dependencies.recyclerView
    implementation dependencies.card
    implementation dependencies.constraintLayout
    implementation dependencies.androidAnnotations

    implementation dependencies.dagger
    kapt dependencies.daggerCompiler

    implementation dependencies.rxKotlin
    implementation dependencies.rxAndroid
    implementation dependencies.rxBindingsSupport
    implementation dependencies.rxBindings
    implementation dependencies.rxBroadcast

    implementation dependencies.glide
    kapt dependencies.glideAnnontation

    testImplementation testDependencies.junit

    androidTestImplementation testDependencies.runner
    androidTestImplementation testDependencies.espresso
    androidTestImplementation testDependencies.espressoContrib
    androidTestImplementation testDependencies.espressoIntents
}
